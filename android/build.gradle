//used to fetch package.json file
import groovy.json.JsonSlurper

buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.3.0"
    }
}

repositories {
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url("$rootDir/../node_modules/react-native/android")
    }
    maven { url "https://android-sdk.voxeet.com/release" }
    maven { url "http://dl.bintray.com/voxeet/maven" }
    google()
    mavenCentral()
    jcenter()
}

apply plugin: 'com.android.library'

def ext = rootProject.ext
def _compile = ext.has("compileSdkVersion") ? ext.compileSdkVersion : 29
def _buildTools = ext.has("buildToolsVersion") ? ext.buildToolsVersion : "29.0.3"
def _minSdk = ext.has("minSdkVersion") ? ext.minSdkVersion : 21
def _targetSdk = ext.has("targetSdkVersion") ? ext.targetSdkVersion : 29

if(_minSdk < 21) _minSdk = 21

def packageJson = new JsonSlurper().parse file("../package.json")
def version = packageJson.version

android {
    compileSdkVersion _compile
    buildToolsVersion _buildTools

    defaultConfig {
        minSdkVersion _minSdk
        targetSdkVersion _targetSdk
        versionCode 1
        versionName "1.0"
        buildConfigField "String", "VOXEET_REACT_NATIVE_VERSION", "\"${version}\""
    }
    lintOptions {
        abortOnError false
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation "com.facebook.react:react-native:+"
    implementation "androidx.appcompat:appcompat:1.1.0"
    implementation "androidx.annotation:annotation:1.1.0"


    //add by default the optional firebase dependency
    implementation "com.voxeet.sdk:uxkit-firebase:3.2.1"

    //use the latest toolkit
    implementation ('com.voxeet.sdk:uxkit:3.2.1') { transitive = true }
    implementation ('com.voxeet.sdk:sdk:3.2.2') { transitive = true }
}
